# This file defines the "cmessage" function, which functions as a colorized
# version of the standard "message" function

function(cmessage)
    if(NOT WIN32)
        string(ASCII 27 Esc)
        set(ColorReset "${Esc}[m")
        set(ColorBold  "${Esc}[1m")
        set(Red         "${Esc}[31m")
        set(Green       "${Esc}[32m")
        set(Yellow      "${Esc}[33m")
        set(Blue        "${Esc}[34m")
        set(Magenta     "${Esc}[35m")
        set(Cyan        "${Esc}[36m")
        set(White       "${Esc}[37m")
        set(BoldRed     "${Esc}[1;31m")
        set(BoldGreen   "${Esc}[1;32m")
        set(BoldYellow  "${Esc}[1;33m")
        set(BoldBlue    "${Esc}[1;34m")
        set(BoldMagenta "${Esc}[1;35m")
        set(BoldCyan    "${Esc}[1;36m")
        set(BoldWhite   "${Esc}[1;37m")
    endif()
    list(GET ARGV 0 MessageType)
    list(REMOVE_AT ARGV 0)
    if(MessageType STREQUAL FATAL_ERROR OR MessageType STREQUAL SEND_ERROR)
        message(${MessageType} "${BoldRed}${ARGV}${ColorReset}")
    elseif(MessageType STREQUAL WARNING)
        message(${MessageType} "${BoldYellow}${ARGV}${ColorReset}")
    elseif(MessageType STREQUAL AUTHOR_WARNING)
        message(${MessageType} "${BoldCyan}${ARGV}${ColorReset}")
    elseif(MessageType STREQUAL STATUS)
        message(${MessageType} "${Green}${ARGV}${ColorReset}")
    else()
        string(TOUPPER MessageType ${MessageType})
        if(MessageType STREQUAL "RED")
            message("${Red}${ARGV}${ColorReset}")
        elseif(MessageType STREQUAL "GREEN")
            message("${Green}${ARGV}${ColorReset}")
        elseif(MessageType STREQUAL "YELLOW")
            message("${Yellow}${ARGV}${ColorReset}")
        elseif(MessageType STREQUAL "BLUE")
            message("${Blue}${ARGV}${ColorReset}")
        elseif(MessageType STREQUAL "MAGENTA")
            message("${Magenta}${ARGV}${ColorReset}")
        elseif(MessageType STREQUAL "CYAN")
            message("${Cyan}${ARGV}${ColorReset}")
        elseif(MessageType STREQUAL "WHITE")
            message("${White}${ARGV}${ColorReset}")
        elseif(MessageType STREQUAL "BOLD")
            message("${ColorBold}${ARGV}${ColorReset}")
        else()
            message(${MessageType} ${ARGV})
        endif()
    endif()
endfunction()

# vim: sw=4 expandtab
